<!--
CO_OP_TRANSLATOR_METADATA:
{
  "original_hash": "3f92edf2975175577174910caca4a389",
  "translation_date": "2025-08-28T19:30:01+00:00",
  "source_file": "6-consumer/lessons/1-speech-recognition/wio-terminal-speech-to-text.md",
  "language_code": "lt"
}
-->
# Kalbos pavertimas tekstu - Wio Terminal

Å ioje pamokos dalyje raÅ¡ysite kodÄ…, kuris uÅ¾fiksuotÄ… garsÄ… pavers tekstu naudodamas kalbos paslaugÄ….

## SiÅ³skite garsÄ… Ä¯ kalbos paslaugÄ…

GarsÄ… galima siÅ³sti Ä¯ kalbos paslaugÄ… naudojant REST API. Norint naudotis kalbos paslauga, pirmiausia reikia gauti prieigos Å¾etonÄ…, o tada naudoti tÄ… Å¾etonÄ… REST API pasiekimui. Å ie prieigos Å¾etonai galioja tik 10 minuÄiÅ³, todÄ—l jÅ«sÅ³ kodas turÄ—tÅ³ reguliariai juos atnaujinti, kad jie visada bÅ«tÅ³ galiojantys.

### UÅ¾duotis - gauti prieigos Å¾etonÄ…

1. Atidarykite projektÄ… `smart-timer`, jei jis dar neatidarytas.

1. PridÄ—kite Å¡ias bibliotekÅ³ priklausomybes Ä¯ `platformio.ini` failÄ…, kad galÄ—tumÄ—te naudotis WiFi ir apdoroti JSON:

    ```ini
    seeed-studio/Seeed Arduino rpcWiFi @ 1.0.5
    seeed-studio/Seeed Arduino rpcUnified @ 2.1.3
    seeed-studio/Seeed_Arduino_mbedtls @ 3.0.1
    seeed-studio/Seeed Arduino RTC @ 2.0.0
    bblanchon/ArduinoJson @ 6.17.3
    ```

1. PridÄ—kite Å¡Ä¯ kodÄ… Ä¯ `config.h` antraÅ¡tÄ—s failÄ…:

    ```cpp
    const char *SSID = "<SSID>";
    const char *PASSWORD = "<PASSWORD>";
    
    const char *SPEECH_API_KEY = "<API_KEY>";
    const char *SPEECH_LOCATION = "<LOCATION>";
    const char *LANGUAGE = "<LANGUAGE>";

    const char *TOKEN_URL = "https://%s.api.cognitive.microsoft.com/sts/v1.0/issuetoken";
    ```

    Pakeiskite `<SSID>` ir `<PASSWORD>` Ä¯ atitinkamas jÅ«sÅ³ WiFi reikÅ¡mes.

    Pakeiskite `<API_KEY>` Ä¯ jÅ«sÅ³ kalbos paslaugos iÅ¡tekliaus API raktÄ…. Pakeiskite `<LOCATION>` Ä¯ vietÄ…, kuriÄ… nurodÄ—te kurdami kalbos paslaugos iÅ¡teklÄ¯.

    Pakeiskite `<LANGUAGE>` Ä¯ kalbos, kuria kalbÄ—site, lokalÄ—s pavadinimÄ…, pavyzdÅ¾iui, `en-GB` anglÅ³ kalbai arba `zn-HK` kantonieÄiÅ³ kalbai. PalaikomÅ³ kalbÅ³ ir jÅ³ lokalÄ—s pavadinimÅ³ sÄ…raÅ¡Ä… galite rasti [Microsoft dokumentacijoje apie kalbÅ³ ir balsÅ³ palaikymÄ…](https://docs.microsoft.com/azure/cognitive-services/speech-service/language-support?WT.mc_id=academic-17441-jabenn#speech-to-text).

    KonstantÄ… `TOKEN_URL` sudaro Å¾etono iÅ¡davimo paslaugos URL be vietos. VÄ—liau tai bus sujungta su vieta, kad bÅ«tÅ³ gautas pilnas URL.

1. Kaip ir jungiantis prie Custom Vision, jums reikÄ—s naudoti HTTPS ryÅ¡Ä¯, kad prisijungtumÄ—te prie Å¾etono iÅ¡davimo paslaugos. PridÄ—kite Å¡Ä¯ kodÄ… Ä¯ `config.h` failo pabaigÄ…:

    ```cpp
    const char *TOKEN_CERTIFICATE =
        "-----BEGIN CERTIFICATE-----\r\n"
        "MIIF8zCCBNugAwIBAgIQAueRcfuAIek/4tmDg0xQwDANBgkqhkiG9w0BAQwFADBh\r\n"
        "MQswCQYDVQQGEwJVUzEVMBMGA1UEChMMRGlnaUNlcnQgSW5jMRkwFwYDVQQLExB3\r\n"
        "d3cuZGlnaWNlcnQuY29tMSAwHgYDVQQDExdEaWdpQ2VydCBHbG9iYWwgUm9vdCBH\r\n"
        "MjAeFw0yMDA3MjkxMjMwMDBaFw0yNDA2MjcyMzU5NTlaMFkxCzAJBgNVBAYTAlVT\r\n"
        "MR4wHAYDVQQKExVNaWNyb3NvZnQgQ29ycG9yYXRpb24xKjAoBgNVBAMTIU1pY3Jv\r\n"
        "c29mdCBBenVyZSBUTFMgSXNzdWluZyBDQSAwNjCCAiIwDQYJKoZIhvcNAQEBBQAD\r\n"
        "ggIPADCCAgoCggIBALVGARl56bx3KBUSGuPc4H5uoNFkFH4e7pvTCxRi4j/+z+Xb\r\n"
        "wjEz+5CipDOqjx9/jWjskL5dk7PaQkzItidsAAnDCW1leZBOIi68Lff1bjTeZgMY\r\n"
        "iwdRd3Y39b/lcGpiuP2d23W95YHkMMT8IlWosYIX0f4kYb62rphyfnAjYb/4Od99\r\n"
        "ThnhlAxGtfvSbXcBVIKCYfZgqRvV+5lReUnd1aNjRYVzPOoifgSx2fRyy1+pO1Uz\r\n"
        "aMMNnIOE71bVYW0A1hr19w7kOb0KkJXoALTDDj1ukUEDqQuBfBxReL5mXiu1O7WG\r\n"
        "0vltg0VZ/SZzctBsdBlx1BkmWYBW261KZgBivrql5ELTKKd8qgtHcLQA5fl6JB0Q\r\n"
        "gs5XDaWehN86Gps5JW8ArjGtjcWAIP+X8CQaWfaCnuRm6Bk/03PQWhgdi84qwA0s\r\n"
        "sRfFJwHUPTNSnE8EiGVk2frt0u8PG1pwSQsFuNJfcYIHEv1vOzP7uEOuDydsmCjh\r\n"
        "lxuoK2n5/2aVR3BMTu+p4+gl8alXoBycyLmj3J/PUgqD8SL5fTCUegGsdia/Sa60\r\n"
        "N2oV7vQ17wjMN+LXa2rjj/b4ZlZgXVojDmAjDwIRdDUujQu0RVsJqFLMzSIHpp2C\r\n"
        "Zp7mIoLrySay2YYBu7SiNwL95X6He2kS8eefBBHjzwW/9FxGqry57i71c2cDAgMB\r\n"
        "AAGjggGtMIIBqTAdBgNVHQ4EFgQU1cFnOsKjnfR3UltZEjgp5lVou6UwHwYDVR0j\r\n"
        "BBgwFoAUTiJUIBiV5uNu5g/6+rkS7QYXjzkwDgYDVR0PAQH/BAQDAgGGMB0GA1Ud\r\n"
        "JQQWMBQGCCsGAQUFBwMBBggrBgEFBQcDAjASBgNVHRMBAf8ECDAGAQH/AgEAMHYG\r\n"
        "CCsGAQUFBwEBBGowaDAkBggrBgEFBQcwAYYYaHR0cDovL29jc3AuZGlnaWNlcnQu\r\n"
        "Y29tMEAGCCsGAQUFBzAChjRodHRwOi8vY2FjZXJ0cy5kaWdpY2VydC5jb20vRGln\r\n"
        "aUNlcnRHbG9iYWxSb290RzIuY3J0MHsGA1UdHwR0MHIwN6A1oDOGMWh0dHA6Ly9j\r\n"
        "cmwzLmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5jcmwwN6A1oDOG\r\n"
        "MWh0dHA6Ly9jcmw0LmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5j\r\n"
        "cmwwHQYDVR0gBBYwFDAIBgZngQwBAgEwCAYGZ4EMAQICMBAGCSsGAQQBgjcVAQQD\r\n"
        "AgEAMA0GCSqGSIb3DQEBDAUAA4IBAQB2oWc93fB8esci/8esixj++N22meiGDjgF\r\n"
        "+rA2LUK5IOQOgcUSTGKSqF9lYfAxPjrqPjDCUPHCURv+26ad5P/BYtXtbmtxJWu+\r\n"
        "cS5BhMDPPeG3oPZwXRHBJFAkY4O4AF7RIAAUW6EzDflUoDHKv83zOiPfYGcpHc9s\r\n"
        "kxAInCedk7QSgXvMARjjOqdakor21DTmNIUotxo8kHv5hwRlGhBJwps6fEVi1Bt0\r\n"
        "trpM/3wYxlr473WSPUFZPgP1j519kLpWOJ8z09wxay+Br29irPcBYv0GMXlHqThy\r\n"
        "8y4m/HyTQeI2IMvMrQnwqPpY+rLIXyviI2vLoI+4xKE4Rn38ZZ8m\r\n"
        "-----END CERTIFICATE-----\r\n";
    ```

    Tai tas pats sertifikatas, kurÄ¯ naudojote jungdamiesi prie Custom Vision.

1. PridÄ—kite `WiFi` antraÅ¡tÄ—s failo ir `config` antraÅ¡tÄ—s failo Ä¯traukimÄ… Ä¯ `main.cpp` failo virÅ¡Å³:

    ```cpp
    #include <rpcWiFi.h>

    #include "config.h"
    ```

1. PridÄ—kite kodÄ…, kuris prisijungia prie WiFi, Ä¯ `main.cpp` virÅ¡ `setup` funkcijos:

    ```cpp
    void connectWiFi()
    {
        while (WiFi.status() != WL_CONNECTED)
        {
            Serial.println("Connecting to WiFi..");
            WiFi.begin(SSID, PASSWORD);
            delay(500);
        }
    
        Serial.println("Connected!");
    }
    ```

1. IÅ¡kvieskite Å¡iÄ… funkcijÄ… iÅ¡ `setup` funkcijos po to, kai bus uÅ¾megztas serijinis ryÅ¡ys:

    ```cpp
    connectWiFi();
    ```

1. Sukurkite naujÄ… antraÅ¡tÄ—s failÄ… `src` aplanke, pavadintÄ… `speech_to_text.h`. Å iame antraÅ¡tÄ—s faile pridÄ—kite Å¡Ä¯ kodÄ…:

    ```cpp
    #pragma once
    
    #include <Arduino.h>
    #include <ArduinoJson.h>
    #include <HTTPClient.h>
    #include <WiFiClientSecure.h>
    
    #include "config.h"
    #include "mic.h"
    
    class SpeechToText
    {
    public:

    private:

    };

    SpeechToText speechToText;
    ```

    Tai apima bÅ«tinus antraÅ¡tÄ—s failus HTTP ryÅ¡iui, konfigÅ«racijai ir `mic.h` antraÅ¡tÄ—s failÄ…, taip pat apibrÄ—Å¾ia klasÄ™ `SpeechToText`, prieÅ¡ paskelbiant Å¡ios klasÄ—s egzemplioriÅ³, kuris bus naudojamas vÄ—liau.

1. PridÄ—kite Å¡iuos 2 laukus Ä¯ Å¡ios klasÄ—s `private` sekcijÄ…:

    ```cpp
    WiFiClientSecure _token_client;
    String _access_token;
    ```

    `_token_client` yra WiFi klientas, naudojantis HTTPS ir bus naudojamas prieigos Å¾etonui gauti. Å is Å¾etonas bus saugomas `_access_token`.

1. PridÄ—kite Å¡Ä¯ metodÄ… Ä¯ `private` sekcijÄ…:

    ```cpp
    String getAccessToken()
    {
        char url[128];
        sprintf(url, TOKEN_URL, SPEECH_LOCATION);
    
        HTTPClient httpClient;
        httpClient.begin(_token_client, url);
    
        httpClient.addHeader("Ocp-Apim-Subscription-Key", SPEECH_API_KEY);
        int httpResultCode = httpClient.POST("{}");
    
        if (httpResultCode != 200)
        {
            Serial.println("Error getting access token, trying again...");
            delay(10000);
            return getAccessToken();
        }
    
        Serial.println("Got access token.");
        String result = httpClient.getString();
    
        httpClient.end();
    
        return result;
    }
    ```

    Å is kodas sukuria URL Å¾etono iÅ¡davimo API, naudodamas kalbos iÅ¡tekliaus vietÄ…. Tada jis sukuria `HTTPClient`, kad atliktÅ³ Å¾iniatinklio uÅ¾klausÄ…, nustatydamas jÄ¯ naudoti WiFi klientÄ…, sukonfigÅ«ruotÄ… su Å¾etono galiniÅ³ taÅ¡kÅ³ sertifikatu. Jis nustato API raktÄ… kaip antraÅ¡tÄ™ uÅ¾klausai. Tada jis atlieka POST uÅ¾klausÄ…, kad gautÅ³ sertifikatÄ…, pakartodamas, jei gauna klaidÅ³. Galiausiai grÄ…Å¾inamas prieigos Å¾etonas.

1. Ä® `public` sekcijÄ… pridÄ—kite metodÄ…, skirtÄ… gauti prieigos Å¾etonÄ…. Jis bus reikalingas vÄ—lesnÄ—se pamokose tekstui paversti Ä¯ kalbÄ….

    ```cpp
    String AccessToken()
    {
        return _access_token;
    }
    ```

1. Ä® `public` sekcijÄ… pridÄ—kite `init` metodÄ…, kuris nustato Å¾etono klientÄ…:

    ```cpp
    void init()
    {
        _token_client.setCACert(TOKEN_CERTIFICATE);
        _access_token = getAccessToken();
    }
    ```

    Tai nustato sertifikatÄ… WiFi klientui, tada gauna prieigos Å¾etonÄ….

1. `main.cpp` faile pridÄ—kite Å¡Ä¯ naujÄ… antraÅ¡tÄ—s failÄ… Ä¯ Ä¯traukimÅ³ direktyvas:

    ```cpp
    #include "speech_to_text.h"
    ```

1. Inicializuokite `SpeechToText` klasÄ™ `setup` funkcijos pabaigoje, po `mic.init` iÅ¡kvietimo, bet prieÅ¡ `Ready` iÅ¡vedimÄ… Ä¯ serijinÄ¯ monitoriÅ³:

    ```cpp
    speechToText.init();
    ```

### UÅ¾duotis - skaityti garsÄ… iÅ¡ flash atminties

1. AnkstesnÄ—je Å¡ios pamokos dalyje garsas buvo Ä¯raÅ¡ytas Ä¯ flash atmintÄ¯. Å is garsas turÄ—s bÅ«ti siunÄiamas Ä¯ Kalbos paslaugÅ³ REST API, todÄ—l jÄ¯ reikia perskaityti iÅ¡ flash atminties. Jo negalima Ä¯kelti Ä¯ atminties buferÄ¯, nes jis bÅ«tÅ³ per didelis. `HTTPClient` klasÄ—, kuri atlieka REST uÅ¾klausas, gali perduoti duomenis naudodama Arduino Stream - klasÄ™, kuri gali Ä¯kelti duomenis maÅ¾ais gabalÄ—liais, siÅ³sdama juos po vienÄ… kaip uÅ¾klausos dalÄ¯. KiekvienÄ… kartÄ…, kai iÅ¡kvieÄiate `read` ant srauto, jis grÄ…Å¾ina kitÄ… duomenÅ³ blokÄ…. Arduino srautas gali bÅ«ti sukurtas, kad galÄ—tÅ³ skaityti iÅ¡ flash atminties. Sukurkite naujÄ… failÄ…, pavadintÄ… `flash_stream.h`, `src` aplanke, ir pridÄ—kite Å¡Ä¯ kodÄ…:

    ```cpp
    #pragma once
    
    #include <Arduino.h>
    #include <HTTPClient.h>
    #include <sfud.h>

    #include "config.h"
    
    class FlashStream : public Stream
    {
    public:
        virtual size_t write(uint8_t val)
        {    
        }
    
        virtual int available()
        {
        }
    
        virtual int read()
        {
        }
    
        virtual int peek()
        {
        }
    private:
    
    };
    ```

    Tai deklaruoja `FlashStream` klasÄ™, paveldinÄiÄ… iÅ¡ Arduino `Stream` klasÄ—s. Tai yra abstrakti klasÄ— - paveldinÄios klasÄ—s turi Ä¯gyvendinti kelis metodus, kol klasÄ— gali bÅ«ti instancijuota, ir Å¡ie metodai yra apibrÄ—Å¾ti Å¡ioje klasÄ—je.

    âœ… Daugiau apie Arduino srautus skaitykite [Arduino Stream dokumentacijoje](https://www.arduino.cc/reference/en/language/functions/communication/stream/)

1. PridÄ—kite Å¡iuos laukus Ä¯ `private` sekcijÄ…:

    ```cpp
    size_t _pos;
    size_t _flash_address;
    const sfud_flash *_flash;

    byte _buffer[HTTP_TCP_BUFFER_SIZE];
    ```

    Tai apibrÄ—Å¾ia laikinÄ… buferÄ¯ duomenims, skaitomiems iÅ¡ flash atminties, saugoti, kartu su laukais, skirtais dabartinei pozicijai buferyje ir dabartiniam adresui flash atmintyje saugoti.

1. `private` sekcijoje pridÄ—kite Å¡Ä¯ metodÄ…:

    ```cpp
    void populateBuffer()
    {
        sfud_read(_flash, _flash_address, HTTP_TCP_BUFFER_SIZE, _buffer);
        _flash_address += HTTP_TCP_BUFFER_SIZE;
        _pos = 0;
    }
    ```

    Å is kodas skaito iÅ¡ flash atminties dabartiniu adresu ir saugo duomenis buferyje. Tada jis padidina adresÄ…, kad kitas iÅ¡kvietimas skaitytÅ³ kitÄ… atminties blokÄ…. Buferio dydis nustatytas pagal didÅ¾iausiÄ… gabalÄ…, kurÄ¯ `HTTPClient` siunÄia REST API vienu metu.

    > ğŸ’ Flash atminties trynimas turi bÅ«ti atliekamas naudojant grÅ«dÅ³ dydÄ¯, taÄiau skaitymas to nereikalauja.

1. `public` sekcijoje Å¡ios klasÄ—s pridÄ—kite konstruktoriÅ³:

    ```cpp
    FlashStream()
    {
        _pos = 0;
        _flash_address = 0;
        _flash = sfud_get_device_table() + 0;
        
        populateBuffer();
    }
    ```

    Å is konstruktorius nustato visus laukus, kad pradÄ—tÅ³ skaityti nuo flash atminties bloko pradÅ¾ios, ir Ä¯kelia pirmÄ… duomenÅ³ gabalÄ… Ä¯ buferÄ¯.

1. Ä®gyvendinkite `write` metodÄ…. Å is srautas tik skaitys duomenis, todÄ—l Å¡is metodas nieko nedaro ir grÄ…Å¾ina 0:

    ```cpp
    virtual size_t write(uint8_t val)
    {
        return 0;
    }
    ```

1. Ä®gyvendinkite `peek` metodÄ…. Å is metodas grÄ…Å¾ina duomenis dabartinÄ—je pozicijoje, nejudindamas srauto Ä¯ priekÄ¯. Keli `peek` iÅ¡kvietimai visada grÄ…Å¾ins tuos paÄius duomenis, jei iÅ¡ srauto nebus skaitoma.

    ```cpp
    virtual int peek()
    {
        return _buffer[_pos];
    }
    ```

1. Ä®gyvendinkite `available` funkcijÄ…. Å i funkcija grÄ…Å¾ina, kiek baitÅ³ galima skaityti iÅ¡ srauto, arba -1, jei srautas baigtas. Å iai klasei maksimalus galimas dydis nevirÅ¡ys `HTTPClient` gabalo dydÅ¾io. Kai Å¡is srautas naudojamas HTTP kliente, jis iÅ¡kvieÄia Å¡iÄ… funkcijÄ…, kad pamatytÅ³, kiek duomenÅ³ yra prieinama, tada praÅ¡o tiek duomenÅ³ siÅ³sti REST API. Nenorime, kad kiekvienas gabalas bÅ«tÅ³ didesnis nei HTTP kliento gabalo dydis, todÄ—l jei yra daugiau duomenÅ³, grÄ…Å¾inamas gabalo dydis. Jei maÅ¾iau, grÄ…Å¾inama, kiek yra prieinama. Kai visi duomenys perduoti, grÄ…Å¾inama -1.

    ```cpp
    virtual int available()
    {
        int remaining = BUFFER_SIZE - ((_flash_address - HTTP_TCP_BUFFER_SIZE) + _pos);
        int bytes_available = min(HTTP_TCP_BUFFER_SIZE, remaining);

        if (bytes_available == 0)
        {
            bytes_available = -1;
        }

        return bytes_available;
    }
    ```

1. Ä®gyvendinkite `read` metodÄ…, kad grÄ…Å¾intÅ³ kitÄ… baitÄ… iÅ¡ buferio, padidindamas pozicijÄ…. Jei pozicija virÅ¡ija buferio dydÄ¯, jis uÅ¾pildo buferÄ¯ kitu bloku iÅ¡ flash atminties ir atstato pozicijÄ….

    ```cpp
    virtual int read()
    {
        int retVal = _buffer[_pos++];

        if (_pos == HTTP_TCP_BUFFER_SIZE)
        {
            populateBuffer();
        }

        return retVal;
    }
    ```

1. `speech_to_text.h` antraÅ¡tÄ—s faile pridÄ—kite Ä¯traukimo direktyvÄ… Å¡iam naujam antraÅ¡tÄ—s failui:

    ```cpp
    #include "flash_stream.h"
    ```

### UÅ¾duotis - paversti kalbÄ… tekstu

1. Kalba gali bÅ«ti paversta tekstu siunÄiant garsÄ… Ä¯ Kalbos paslaugÄ… per REST API. Å is REST API turi kitÄ… sertifikatÄ… nei Å¾etono iÅ¡davÄ—jas, todÄ—l pridÄ—kite Å¡Ä¯ kodÄ… Ä¯ `config.h` antraÅ¡tÄ—s failÄ…, kad apibrÄ—Å¾tumÄ—te Å¡Ä¯ sertifikatÄ…:

    ```cpp
    const char *SPEECH_CERTIFICATE =
        "-----BEGIN CERTIFICATE-----\r\n"
        "MIIF8zCCBNugAwIBAgIQCq+mxcpjxFFB6jvh98dTFzANBgkqhkiG9w0BAQwFADBh\r\n"
        "MQswCQYDVQQGEwJVUzEVMBMGA1UEChMMRGlnaUNlcnQgSW5jMRkwFwYDVQQLExB3\r\n"
        "d3cuZGlnaWNlcnQuY29tMSAwHgYDVQQDExdEaWdpQ2VydCBHbG9iYWwgUm9vdCBH\r\n"
        "MjAeFw0yMDA3MjkxMjMwMDBaFw0yNDA2MjcyMzU5NTlaMFkxCzAJBgNVBAYTAlVT\r\n"
        "MR4wHAYDVQQKExVNaWNyb3NvZnQgQ29ycG9yYXRpb24xKjAoBgNVBAMTIU1pY3Jv\r\n"
        "c29mdCBBenVyZSBUTFMgSXNzdWluZyBDQSAwMTCCAiIwDQYJKoZIhvcNAQEBBQAD\r\n"
        "ggIPADCCAgoCggIBAMedcDrkXufP7pxVm1FHLDNA9IjwHaMoaY8arqqZ4Gff4xyr\r\n"
        "RygnavXL7g12MPAx8Q6Dd9hfBzrfWxkF0Br2wIvlvkzW01naNVSkHp+OS3hL3W6n\r\n"
        "l/jYvZnVeJXjtsKYcXIf/6WtspcF5awlQ9LZJcjwaH7KoZuK+THpXCMtzD8XNVdm\r\n"
        "GW/JI0C/7U/E7evXn9XDio8SYkGSM63aLO5BtLCv092+1d4GGBSQYolRq+7Pd1kR\r\n"
        "EkWBPm0ywZ2Vb8GIS5DLrjelEkBnKCyy3B0yQud9dpVsiUeE7F5sY8Me96WVxQcb\r\n"
        "OyYdEY/j/9UpDlOG+vA+YgOvBhkKEjiqygVpP8EZoMMijephzg43b5Qi9r5UrvYo\r\n"
        "o19oR/8pf4HJNDPF0/FJwFVMW8PmCBLGstin3NE1+NeWTkGt0TzpHjgKyfaDP2tO\r\n"
        "4bCk1G7pP2kDFT7SYfc8xbgCkFQ2UCEXsaH/f5YmpLn4YPiNFCeeIida7xnfTvc4\r\n"
        "7IxyVccHHq1FzGygOqemrxEETKh8hvDR6eBdrBwmCHVgZrnAqnn93JtGyPLi6+cj\r\n"
        "WGVGtMZHwzVvX1HvSFG771sskcEjJxiQNQDQRWHEh3NxvNb7kFlAXnVdRkkvhjpR\r\n"
        "GchFhTAzqmwltdWhWDEyCMKC2x/mSZvZtlZGY+g37Y72qHzidwtyW7rBetZJAgMB\r\n"
        "AAGjggGtMIIBqTAdBgNVHQ4EFgQUDyBd16FXlduSzyvQx8J3BM5ygHYwHwYDVR0j\r\n"
        "BBgwFoAUTiJUIBiV5uNu5g/6+rkS7QYXjzkwDgYDVR0PAQH/BAQDAgGGMB0GA1Ud\r\n"
        "JQQWMBQGCCsGAQUFBwMBBggrBgEFBQcDAjASBgNVHRMBAf8ECDAGAQH/AgEAMHYG\r\n"
        "CCsGAQUFBwEBBGowaDAkBggrBgEFBQcwAYYYaHR0cDovL29jc3AuZGlnaWNlcnQu\r\n"
        "Y29tMEAGCCsGAQUFBzAChjRodHRwOi8vY2FjZXJ0cy5kaWdpY2VydC5jb20vRGln\r\n"
        "aUNlcnRHbG9iYWxSb290RzIuY3J0MHsGA1UdHwR0MHIwN6A1oDOGMWh0dHA6Ly9j\r\n"
        "cmwzLmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5jcmwwN6A1oDOG\r\n"
        "MWh0dHA6Ly9jcmw0LmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5j\r\n"
        "cmwwHQYDVR0gBBYwFDAIBgZngQwBAgEwCAYGZ4EMAQICMBAGCSsGAQQBgjcVAQQD\r\n"
        "AgEAMA0GCSqGSIb3DQEBDAUAA4IBAQAlFvNh7QgXVLAZSsNR2XRmIn9iS8OHFCBA\r\n"
        "WxKJoi8YYQafpMTkMqeuzoL3HWb1pYEipsDkhiMnrpfeYZEA7Lz7yqEEtfgHcEBs\r\n"
        "K9KcStQGGZRfmWU07hPXHnFz+5gTXqzCE2PBMlRgVUYJiA25mJPXfB00gDvGhtYa\r\n"
        "+mENwM9Bq1B9YYLyLjRtUz8cyGsdyTIG/bBM/Q9jcV8JGqMU/UjAdh1pFyTnnHEl\r\n"
        "Y59Npi7F87ZqYYJEHJM2LGD+le8VsHjgeWX2CJQko7klXvcizuZvUEDTjHaQcs2J\r\n"
        "+kPgfyMIOY1DMJ21NxOJ2xPRC/wAh/hzSBRVtoAnyuxtkZ4VjIOh\r\n"
        "-----END CERTIFICATE-----\r\n";
    ```

1. PridÄ—kite konstantÄ… Å¡iame faile kalbos URL be vietos. Tai bus sujungta su vieta ir kalba vÄ—liau, kad bÅ«tÅ³ gautas pilnas URL.

    ```cpp
    const char *SPEECH_URL = "https://%s.stt.speech.microsoft.com/speech/recognition/conversation/cognitiveservices/v1?language=%s";
    ```

1. `speech_to_text.h` antraÅ¡tÄ—s faile, `private` sekcijoje `SpeechToText` klasÄ—s, apibrÄ—Å¾kite laukÄ… WiFi klientui, naudojanÄiam kalbos sertifikatÄ…:

    ```cpp
    WiFiClientSecure _speech_client;
    ```

1. `init` metode nustatykite Å¡io WiFi kliento sertifikatÄ…:

    ```cpp
    _speech_client.setCACert(SPEECH_CERTIFICATE);
    ```

1. PridÄ—kite Å¡Ä¯ kodÄ… Ä¯ `public` sekcijÄ… `SpeechToText` klasÄ—s, kad apibrÄ—Å¾tumÄ—te metodÄ… kalbai paversti tekstu:

    ```cpp
    String convertSpeechToText()
    {
        
    }
    ```

1. PridÄ—kite Å¡Ä¯ kodÄ… Ä¯ Å¡Ä¯ metodÄ…, kad sukurtumÄ—te HTTP klientÄ…, naudojantÄ¯ WiFi klientÄ…, sukonfigÅ«ruotÄ… su kalbos sertifikatu, ir naudodami kalbos URL, nustatytÄ… su vieta ir kalba:

    ```cpp
    char url[128];
    sprintf(url, SPEECH_URL, SPEECH_LOCATION, LANGUAGE);

    HTTPClient httpClient;
    httpClient.begin(_speech_client, url);
    ```

1. Kai kurios antraÅ¡tÄ—s turi bÅ«ti nustatytos ryÅ¡iui:

    ```cpp
    httpClient.addHeader("Authorization", String("Bearer ") + _access_token);
    httpClient.addHeader("Content-Type", String("audio/wav; codecs=audio/pcm; samplerate=") + String(RATE));
    httpClient.addHeader("Accept", "application/json;text/xml");
    ```

    Tai nustato antraÅ¡tes autorizacijai naudojant prieigos Å¾etonÄ…, garso formatui naudojant mÄ—giniÅ³ Ä—mimo daÅ¾nÄ¯ ir nustato, kad klientas tikisi rezultato JSON formatu.

1. Po to pridÄ—kite Å¡Ä¯ kodÄ…, kad atliktumÄ—te REST API uÅ¾klausÄ…:

    ```cpp
    Serial.println("Sending speech...");

    FlashStream stream;
    int httpResponseCode = httpClient.sendRequest("POST", &stream, BUFFER_SIZE);

    Serial.println("Speech sent!");
    ```

    Tai sukuria `FlashStream` ir naudoja jÄ¯ duomenims perduoti REST API.

1. Po to pridÄ—kite Å¡Ä¯ kodÄ…:

    ```cpp
    String text = "";

    if (httpResponseCode == 200)
    {
        String result = httpClient.getString();
        Serial.println(result);

        DynamicJsonDocument doc(1024);
        deserializeJson(doc, result.c_str());

        JsonObject obj = doc.as<JsonObject>();
        text = obj["DisplayText"].as<String>();
    }
    else if (httpResponseCode == 401)
    {
        Serial.println("Access token expired, trying again with a new token");
        _access_token = getAccessToken();
        return convertSpeechToText();
    }
    else
    {
        Serial.print("Failed to convert text to speech - error ");
        Serial.println(httpResponseCode);
    }
    ```

    Å is kodas tikrina atsakymo kodÄ….

    Jei jis yra 200, sÄ—kmÄ—s kodas, tada rezultatas gaunamas, dekoduojamas iÅ¡ JSON, ir `DisplayText` savybÄ— nustatoma Ä¯ `text` kintamÄ…jÄ¯. Tai yra savybÄ—, kurioje grÄ…Å¾inamas kalbos tekstinis variantas.

    Jei atsakymo kodas yra 401, tada prieigos Å¾etonas pasibaigÄ— (Å¡ie Å¾etonai galioja tik 10 minuÄiÅ³). Gaunamas naujas prieigos Å¾etonas ir uÅ¾klausa atliekama dar kartÄ….

    Kitu atveju klaida siunÄiama Ä¯ serijinÄ¯ monitoriÅ³, o `text` lieka tuÅ¡Äias.

1. PridÄ—kite Å¡Ä¯ kodÄ… Å¡io metodo pabaigoje, kad uÅ¾darytumÄ—te HTTP klientÄ… ir grÄ…Å¾intumÄ—te tekstÄ…:

    ```cpp
    httpClient.end();

    return text;
    ```

1. `main.cpp` faile iÅ¡kvieskite Å¡Ä¯ naujÄ… `convertSpeechToText` metodÄ… `processAudio` funkcijoje, tada iÅ¡veskite kalbÄ… Ä¯ serijinÄ¯ monitoriÅ³:

    ```cpp
    String text = speechToText.convertSpeechToText();
    Serial.println(text);
    ```

1. Sukurkite Å¡Ä¯ kodÄ…, Ä¯kelkite jÄ¯ Ä¯ savo Wio Terminal ir iÅ¡bandykite per serijinÄ¯ monitoriÅ³. Kai serijiniame monitore pamatysite `Ready`, paspauskite C mygtukÄ… (kairÄ—je pusÄ—je, arÄiausiai maitinimo jungiklio) ir kalbÄ—kite. 4 sekundÄ—s garso bus uÅ¾fiksuotos, tada paverstos tekstu.

    ```output
    --- Available filters and text transformations: colorize, debug, default, direct, hexlify, log2file, nocontrol, printable, send_on_enter, time
    --- More details at http://bit.ly/pio-monitor-filters
    --- Miniterm on /dev/cu.usbmodem1101  9600,8,N,1 ---
    --- Quit: Ctrl+C | Menu: Ctrl+T | Help: Ctrl+T followed by Ctrl+H ---
    Connecting to WiFi..
    Connected!
    Got access token.
    Ready.
    Starting recording...
    Finished recording
    Sending speech...
    Speech sent!
    {"RecognitionStatus":"Success","DisplayText":"Set a 2 minute and 27 second timer.","Offset":4700000,"Duration":35300000}
    Set a 2 minute and 27 second timer.
    ```

> ğŸ’ Å Ä¯ kodÄ… galite rasti [code-speech-to-text/wio-terminal](../../../../../6-consumer/lessons/1-speech-recognition/code-speech-to-text/wio-terminal) aplanke.

ğŸ˜€ JÅ«sÅ³ kalbos pavertimo tekstu programa buvo sÄ—kminga!

---

**AtsakomybÄ—s apribojimas**:  
Å is dokumentas buvo iÅ¡verstas naudojant dirbtinio intelekto vertimo paslaugÄ… [Co-op Translator](https://github.com/Azure/co-op-translator). Nors siekiame tikslumo, atkreipiame dÄ—mesÄ¯, kad automatiniai vertimai gali turÄ—ti klaidÅ³ ar netikslumÅ³. Originalus dokumentas jo gimtÄ…ja kalba turÄ—tÅ³ bÅ«ti laikomas autoritetingu Å¡altiniu. Kritinei informacijai rekomenduojama naudotis profesionaliÅ³ vertÄ—jÅ³ paslaugomis. Mes neprisiimame atsakomybÄ—s uÅ¾ nesusipratimus ar klaidingus aiÅ¡kinimus, kylanÄius dÄ—l Å¡io vertimo naudojimo.