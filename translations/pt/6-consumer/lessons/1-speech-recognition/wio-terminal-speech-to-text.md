<!--
CO_OP_TRANSLATOR_METADATA:
{
  "original_hash": "3f92edf2975175577174910caca4a389",
  "translation_date": "2025-08-25T22:47:55+00:00",
  "source_file": "6-consumer/lessons/1-speech-recognition/wio-terminal-speech-to-text.md",
  "language_code": "pt"
}
-->
# Fala para texto - Wio Terminal

Nesta parte da li√ß√£o, vais escrever c√≥digo para converter fala captada no √°udio em texto utilizando o servi√ßo de fala.

## Enviar o √°udio para o servi√ßo de fala

O √°udio pode ser enviado para o servi√ßo de fala utilizando a API REST. Para usar o servi√ßo de fala, primeiro precisas de solicitar um token de acesso e, em seguida, usar esse token para aceder √† API REST. Estes tokens de acesso expiram ap√≥s 10 minutos, por isso o teu c√≥digo deve solicit√°-los regularmente para garantir que est√£o sempre atualizados.

### Tarefa - obter um token de acesso

1. Abre o projeto `smart-timer` caso ainda n√£o esteja aberto.

1. Adiciona as seguintes depend√™ncias de biblioteca ao ficheiro `platformio.ini` para aceder ao WiFi e manipular JSON:

    ```ini
    seeed-studio/Seeed Arduino rpcWiFi @ 1.0.5
    seeed-studio/Seeed Arduino rpcUnified @ 2.1.3
    seeed-studio/Seeed_Arduino_mbedtls @ 3.0.1
    seeed-studio/Seeed Arduino RTC @ 2.0.0
    bblanchon/ArduinoJson @ 6.17.3
    ```

1. Adiciona o seguinte c√≥digo ao ficheiro de cabe√ßalho `config.h`:

    ```cpp
    const char *SSID = "<SSID>";
    const char *PASSWORD = "<PASSWORD>";
    
    const char *SPEECH_API_KEY = "<API_KEY>";
    const char *SPEECH_LOCATION = "<LOCATION>";
    const char *LANGUAGE = "<LANGUAGE>";

    const char *TOKEN_URL = "https://%s.api.cognitive.microsoft.com/sts/v1.0/issuetoken";
    ```

    Substitui `<SSID>` e `<PASSWORD>` pelos valores relevantes para o teu WiFi.

    Substitui `<API_KEY>` pela chave de API do recurso do servi√ßo de fala. Substitui `<LOCATION>` pela localiza√ß√£o que utilizaste ao criar o recurso do servi√ßo de fala.

    Substitui `<LANGUAGE>` pelo nome do local para o idioma em que vais falar, por exemplo, `en-GB` para ingl√™s ou `zn-HK` para canton√™s. Podes encontrar uma lista dos idiomas suportados e os seus nomes de local na [documenta√ß√£o de suporte de idiomas e vozes nos Microsoft Docs](https://docs.microsoft.com/azure/cognitive-services/speech-service/language-support?WT.mc_id=academic-17441-jabenn#speech-to-text).

    A constante `TOKEN_URL` √© o URL do emissor de tokens sem a localiza√ß√£o. Este ser√° combinado com a localiza√ß√£o mais tarde para obter o URL completo.

1. Tal como na conex√£o ao Custom Vision, vais precisar de usar uma conex√£o HTTPS para ligar ao servi√ßo emissor de tokens. No final do ficheiro `config.h`, adiciona o seguinte c√≥digo:

    ```cpp
    const char *TOKEN_CERTIFICATE =
        "-----BEGIN CERTIFICATE-----\r\n"
        "MIIF8zCCBNugAwIBAgIQAueRcfuAIek/4tmDg0xQwDANBgkqhkiG9w0BAQwFADBh\r\n"
        "MQswCQYDVQQGEwJVUzEVMBMGA1UEChMMRGlnaUNlcnQgSW5jMRkwFwYDVQQLExB3\r\n"
        "d3cuZGlnaWNlcnQuY29tMSAwHgYDVQQDExdEaWdpQ2VydCBHbG9iYWwgUm9vdCBH\r\n"
        "MjAeFw0yMDA3MjkxMjMwMDBaFw0yNDA2MjcyMzU5NTlaMFkxCzAJBgNVBAYTAlVT\r\n"
        "MR4wHAYDVQQKExVNaWNyb3NvZnQgQ29ycG9yYXRpb24xKjAoBgNVBAMTIU1pY3Jv\r\n"
        "c29mdCBBenVyZSBUTFMgSXNzdWluZyBDQSAwNjCCAiIwDQYJKoZIhvcNAQEBBQAD\r\n"
        "ggIPADCCAgoCggIBALVGARl56bx3KBUSGuPc4H5uoNFkFH4e7pvTCxRi4j/+z+Xb\r\n"
        "wjEz+5CipDOqjx9/jWjskL5dk7PaQkzItidsAAnDCW1leZBOIi68Lff1bjTeZgMY\r\n"
        "iwdRd3Y39b/lcGpiuP2d23W95YHkMMT8IlWosYIX0f4kYb62rphyfnAjYb/4Od99\r\n"
        "ThnhlAxGtfvSbXcBVIKCYfZgqRvV+5lReUnd1aNjRYVzPOoifgSx2fRyy1+pO1Uz\r\n"
        "aMMNnIOE71bVYW0A1hr19w7kOb0KkJXoALTDDj1ukUEDqQuBfBxReL5mXiu1O7WG\r\n"
        "0vltg0VZ/SZzctBsdBlx1BkmWYBW261KZgBivrql5ELTKKd8qgtHcLQA5fl6JB0Q\r\n"
        "gs5XDaWehN86Gps5JW8ArjGtjcWAIP+X8CQaWfaCnuRm6Bk/03PQWhgdi84qwA0s\r\n"
        "sRfFJwHUPTNSnE8EiGVk2frt0u8PG1pwSQsFuNJfcYIHEv1vOzP7uEOuDydsmCjh\r\n"
        "lxuoK2n5/2aVR3BMTu+p4+gl8alXoBycyLmj3J/PUgqD8SL5fTCUegGsdia/Sa60\r\n"
        "N2oV7vQ17wjMN+LXa2rjj/b4ZlZgXVojDmAjDwIRdDUujQu0RVsJqFLMzSIHpp2C\r\n"
        "Zp7mIoLrySay2YYBu7SiNwL95X6He2kS8eefBBHjzwW/9FxGqry57i71c2cDAgMB\r\n"
        "AAGjggGtMIIBqTAdBgNVHQ4EFgQU1cFnOsKjnfR3UltZEjgp5lVou6UwHwYDVR0j\r\n"
        "BBgwFoAUTiJUIBiV5uNu5g/6+rkS7QYXjzkwDgYDVR0PAQH/BAQDAgGGMB0GA1Ud\r\n"
        "JQQWMBQGCCsGAQUFBwMBBggrBgEFBQcDAjASBgNVHRMBAf8ECDAGAQH/AgEAMHYG\r\n"
        "CCsGAQUFBwEBBGowaDAkBggrBgEFBQcwAYYYaHR0cDovL29jc3AuZGlnaWNlcnQu\r\n"
        "Y29tMEAGCCsGAQUFBzAChjRodHRwOi8vY2FjZXJ0cy5kaWdpY2VydC5jb20vRGln\r\n"
        "aUNlcnRHbG9iYWxSb290RzIuY3J0MHsGA1UdHwR0MHIwN6A1oDOGMWh0dHA6Ly9j\r\n"
        "cmwzLmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5jcmwwN6A1oDOG\r\n"
        "MWh0dHA6Ly9jcmw0LmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5j\r\n"
        "cmwwHQYDVR0gBBYwFDAIBgZngQwBAgEwCAYGZ4EMAQICMBAGCSsGAQQBgjcVAQQD\r\n"
        "AgEAMA0GCSqGSIb3DQEBDAUAA4IBAQB2oWc93fB8esci/8esixj++N22meiGDjgF\r\n"
        "+rA2LUK5IOQOgcUSTGKSqF9lYfAxPjrqPjDCUPHCURv+26ad5P/BYtXtbmtxJWu+\r\n"
        "cS5BhMDPPeG3oPZwXRHBJFAkY4O4AF7RIAAUW6EzDflUoDHKv83zOiPfYGcpHc9s\r\n"
        "kxAInCedk7QSgXvMARjjOqdakor21DTmNIUotxo8kHv5hwRlGhBJwps6fEVi1Bt0\r\n"
        "trpM/3wYxlr473WSPUFZPgP1j519kLpWOJ8z09wxay+Br29irPcBYv0GMXlHqThy\r\n"
        "8y4m/HyTQeI2IMvMrQnwqPpY+rLIXyviI2vLoI+4xKE4Rn38ZZ8m\r\n"
        "-----END CERTIFICATE-----\r\n";
    ```

    Este √© o mesmo certificado que utilizaste ao conectar ao Custom Vision.

1. Adiciona um include para o ficheiro de cabe√ßalho do WiFi e o ficheiro de cabe√ßalho de configura√ß√£o no topo do ficheiro `main.cpp`:

    ```cpp
    #include <rpcWiFi.h>

    #include "config.h"
    ```

1. Adiciona c√≥digo para conectar ao WiFi em `main.cpp` acima da fun√ß√£o `setup`:

    ```cpp
    void connectWiFi()
    {
        while (WiFi.status() != WL_CONNECTED)
        {
            Serial.println("Connecting to WiFi..");
            WiFi.begin(SSID, PASSWORD);
            delay(500);
        }
    
        Serial.println("Connected!");
    }
    ```

1. Chama esta fun√ß√£o a partir da fun√ß√£o `setup` ap√≥s a conex√£o serial ter sido estabelecida:

    ```cpp
    connectWiFi();
    ```

1. Cria um novo ficheiro de cabe√ßalho na pasta `src` chamado `speech_to_text.h`. Neste ficheiro de cabe√ßalho, adiciona o seguinte c√≥digo:

    ```cpp
    #pragma once
    
    #include <Arduino.h>
    #include <ArduinoJson.h>
    #include <HTTPClient.h>
    #include <WiFiClientSecure.h>
    
    #include "config.h"
    #include "mic.h"
    
    class SpeechToText
    {
    public:

    private:

    };

    SpeechToText speechToText;
    ```

    Isto inclui alguns ficheiros de cabe√ßalho necess√°rios para uma conex√£o HTTP, configura√ß√£o e o ficheiro de cabe√ßalho `mic.h`, e define uma classe chamada `SpeechToText`, antes de declarar uma inst√¢ncia dessa classe que pode ser usada mais tarde.

1. Adiciona os seguintes 2 campos √† sec√ß√£o `private` desta classe:

    ```cpp
    WiFiClientSecure _token_client;
    String _access_token;
    ```

    O `_token_client` √© um cliente WiFi que usa HTTPS e ser√° usado para obter o token de acesso. Este token ser√° ent√£o armazenado em `_access_token`.

1. Adiciona o seguinte m√©todo √† sec√ß√£o `private`:

    ```cpp
    String getAccessToken()
    {
        char url[128];
        sprintf(url, TOKEN_URL, SPEECH_LOCATION);
    
        HTTPClient httpClient;
        httpClient.begin(_token_client, url);
    
        httpClient.addHeader("Ocp-Apim-Subscription-Key", SPEECH_API_KEY);
        int httpResultCode = httpClient.POST("{}");
    
        if (httpResultCode != 200)
        {
            Serial.println("Error getting access token, trying again...");
            delay(10000);
            return getAccessToken();
        }
    
        Serial.println("Got access token.");
        String result = httpClient.getString();
    
        httpClient.end();
    
        return result;
    }
    ```

    Este c√≥digo constr√≥i o URL para a API do emissor de tokens usando a localiza√ß√£o do recurso de fala. Em seguida, cria um `HTTPClient` para fazer a solicita√ß√£o web, configurando-o para usar o cliente WiFi configurado com o certificado dos endpoints de token. Define a chave de API como um cabe√ßalho para a chamada. Faz uma solicita√ß√£o POST para obter o certificado, tentando novamente se ocorrerem erros. Finalmente, o token de acesso √© retornado.

1. Na sec√ß√£o `public`, adiciona um m√©todo para obter o token de acesso. Este ser√° necess√°rio em li√ß√µes posteriores para converter texto em fala.

    ```cpp
    String AccessToken()
    {
        return _access_token;
    }
    ```

1. Na sec√ß√£o `public`, adiciona um m√©todo `init` que configura o cliente de token:

    ```cpp
    void init()
    {
        _token_client.setCACert(TOKEN_CERTIFICATE);
        _access_token = getAccessToken();
    }
    ```

    Isto define o certificado no cliente WiFi e, em seguida, obt√©m o token de acesso.

1. Em `main.cpp`, adiciona este novo ficheiro de cabe√ßalho √†s diretivas de inclus√£o:

    ```cpp
    #include "speech_to_text.h"
    ```

1. Inicializa a classe `SpeechToText` no final da fun√ß√£o `setup`, ap√≥s a chamada `mic.init`, mas antes de `Ready` ser escrito no monitor serial:

    ```cpp
    speechToText.init();
    ```

### Tarefa - ler √°udio da mem√≥ria flash

1. Numa parte anterior desta li√ß√£o, o √°udio foi gravado na mem√≥ria flash. Este √°udio precisar√° de ser enviado para a API REST do Speech Services, por isso precisa de ser lido da mem√≥ria flash. N√£o pode ser carregado num buffer em mem√≥ria, pois seria demasiado grande. A classe `HTTPClient` que faz chamadas REST pode transmitir dados usando um Arduino Stream - uma classe que pode carregar dados em pequenos blocos, enviando os blocos um de cada vez como parte da solicita√ß√£o. Sempre que chamas `read` num stream, ele retorna o pr√≥ximo bloco de dados. Um stream Arduino pode ser criado para ler da mem√≥ria flash. Cria um novo ficheiro chamado `flash_stream.h` na pasta `src` e adiciona o seguinte c√≥digo:

    ```cpp
    #pragma once
    
    #include <Arduino.h>
    #include <HTTPClient.h>
    #include <sfud.h>

    #include "config.h"
    
    class FlashStream : public Stream
    {
    public:
        virtual size_t write(uint8_t val)
        {    
        }
    
        virtual int available()
        {
        }
    
        virtual int read()
        {
        }
    
        virtual int peek()
        {
        }
    private:
    
    };
    ```

    Isto declara a classe `FlashStream`, derivada da classe `Stream` do Arduino. Esta √© uma classe abstrata - classes derivadas t√™m de implementar alguns m√©todos antes que a classe possa ser instanciada, e esses m√©todos s√£o definidos nesta classe.

    ‚úÖ L√™ mais sobre Streams do Arduino na [documenta√ß√£o de Streams do Arduino](https://www.arduino.cc/reference/en/language/functions/communication/stream/)

1. Adiciona os seguintes campos √† sec√ß√£o `private`:

    ```cpp
    size_t _pos;
    size_t _flash_address;
    const sfud_flash *_flash;

    byte _buffer[HTTP_TCP_BUFFER_SIZE];
    ```

    Isto define um buffer tempor√°rio para armazenar dados lidos da mem√≥ria flash, juntamente com campos para armazenar a posi√ß√£o atual ao ler do buffer, o endere√ßo atual para ler da mem√≥ria flash e o dispositivo de mem√≥ria flash.

1. Na sec√ß√£o `private`, adiciona o seguinte m√©todo:

    ```cpp
    void populateBuffer()
    {
        sfud_read(_flash, _flash_address, HTTP_TCP_BUFFER_SIZE, _buffer);
        _flash_address += HTTP_TCP_BUFFER_SIZE;
        _pos = 0;
    }
    ```

    Este c√≥digo l√™ da mem√≥ria flash no endere√ßo atual e armazena os dados num buffer. Em seguida, incrementa o endere√ßo, para que a pr√≥xima chamada leia o pr√≥ximo bloco de mem√≥ria. O buffer √© dimensionado com base no maior bloco que o `HTTPClient` enviar√° para a API REST de uma s√≥ vez.

    > üíÅ Apagar mem√≥ria flash tem de ser feito usando o tamanho do gr√£o, ler, por outro lado, n√£o.

1. Na sec√ß√£o `public` desta classe, adiciona um construtor:

    ```cpp
    FlashStream()
    {
        _pos = 0;
        _flash_address = 0;
        _flash = sfud_get_device_table() + 0;
        
        populateBuffer();
    }
    ```

    Este construtor configura todos os campos para come√ßar a ler do in√≠cio do bloco de mem√≥ria flash e carrega o primeiro bloco de dados no buffer.

1. Implementa o m√©todo `write`. Este stream apenas ler√° dados, por isso pode n√£o fazer nada e retornar 0:

    ```cpp
    virtual size_t write(uint8_t val)
    {
        return 0;
    }
    ```

1. Implementa o m√©todo `peek`. Este retorna os dados na posi√ß√£o atual sem mover o stream. Chamar `peek` v√°rias vezes sempre retornar√° os mesmos dados enquanto nenhum dado for lido do stream.

    ```cpp
    virtual int peek()
    {
        return _buffer[_pos];
    }
    ```

1. Implementa a fun√ß√£o `available`. Esta retorna quantos bytes podem ser lidos do stream ou -1 se o stream estiver completo. Para esta classe, o m√°ximo dispon√≠vel ser√° no m√°ximo o tamanho do bloco do HTTPClient. Quando este stream √© usado no cliente HTTP, ele chama esta fun√ß√£o para ver quantos dados est√£o dispon√≠veis e, em seguida, solicita essa quantidade de dados para enviar para a API REST. N√£o queremos que cada bloco seja maior do que o tamanho do bloco do cliente HTTP, por isso, se mais do que isso estiver dispon√≠vel, o tamanho do bloco √© retornado. Se menos, ent√£o o que est√° dispon√≠vel √© retornado. Uma vez que todos os dados tenham sido transmitidos, -1 √© retornado.

    ```cpp
    virtual int available()
    {
        int remaining = BUFFER_SIZE - ((_flash_address - HTTP_TCP_BUFFER_SIZE) + _pos);
        int bytes_available = min(HTTP_TCP_BUFFER_SIZE, remaining);

        if (bytes_available == 0)
        {
            bytes_available = -1;
        }

        return bytes_available;
    }
    ```

1. Implementa o m√©todo `read` para retornar o pr√≥ximo byte do buffer, incrementando a posi√ß√£o. Se a posi√ß√£o exceder o tamanho do buffer, ele preenche o buffer com o pr√≥ximo bloco da mem√≥ria flash e redefine a posi√ß√£o.

    ```cpp
    virtual int read()
    {
        int retVal = _buffer[_pos++];

        if (_pos == HTTP_TCP_BUFFER_SIZE)
        {
            populateBuffer();
        }

        return retVal;
    }
    ```

1. No ficheiro de cabe√ßalho `speech_to_text.h`, adiciona uma diretiva de inclus√£o para este novo ficheiro de cabe√ßalho:

    ```cpp
    #include "flash_stream.h"
    ```

### Tarefa - converter a fala em texto

1. A fala pode ser convertida em texto enviando o √°udio para o Servi√ßo de Fala atrav√©s de uma API REST. Esta API REST tem um certificado diferente do emissor de tokens, por isso adiciona o seguinte c√≥digo ao ficheiro de cabe√ßalho `config.h` para definir este certificado:

    ```cpp
    const char *SPEECH_CERTIFICATE =
        "-----BEGIN CERTIFICATE-----\r\n"
        "MIIF8zCCBNugAwIBAgIQCq+mxcpjxFFB6jvh98dTFzANBgkqhkiG9w0BAQwFADBh\r\n"
        "MQswCQYDVQQGEwJVUzEVMBMGA1UEChMMRGlnaUNlcnQgSW5jMRkwFwYDVQQLExB3\r\n"
        "d3cuZGlnaWNlcnQuY29tMSAwHgYDVQQDExdEaWdpQ2VydCBHbG9iYWwgUm9vdCBH\r\n"
        "MjAeFw0yMDA3MjkxMjMwMDBaFw0yNDA2MjcyMzU5NTlaMFkxCzAJBgNVBAYTAlVT\r\n"
        "MR4wHAYDVQQKExVNaWNyb3NvZnQgQ29ycG9yYXRpb24xKjAoBgNVBAMTIU1pY3Jv\r\n"
        "c29mdCBBenVyZSBUTFMgSXNzdWluZyBDQSAwMTCCAiIwDQYJKoZIhvcNAQEBBQAD\r\n"
        "ggIPADCCAgoCggIBAMedcDrkXufP7pxVm1FHLDNA9IjwHaMoaY8arqqZ4Gff4xyr\r\n"
        "RygnavXL7g12MPAx8Q6Dd9hfBzrfWxkF0Br2wIvlvkzW01naNVSkHp+OS3hL3W6n\r\n"
        "l/jYvZnVeJXjtsKYcXIf/6WtspcF5awlQ9LZJcjwaH7KoZuK+THpXCMtzD8XNVdm\r\n"
        "GW/JI0C/7U/E7evXn9XDio8SYkGSM63aLO5BtLCv092+1d4GGBSQYolRq+7Pd1kR\r\n"
        "EkWBPm0ywZ2Vb8GIS5DLrjelEkBnKCyy3B0yQud9dpVsiUeE7F5sY8Me96WVxQcb\r\n"
        "OyYdEY/j/9UpDlOG+vA+YgOvBhkKEjiqygVpP8EZoMMijephzg43b5Qi9r5UrvYo\r\n"
        "o19oR/8pf4HJNDPF0/FJwFVMW8PmCBLGstin3NE1+NeWTkGt0TzpHjgKyfaDP2tO\r\n"
        "4bCk1G7pP2kDFT7SYfc8xbgCkFQ2UCEXsaH/f5YmpLn4YPiNFCeeIida7xnfTvc4\r\n"
        "7IxyVccHHq1FzGygOqemrxEETKh8hvDR6eBdrBwmCHVgZrnAqnn93JtGyPLi6+cj\r\n"
        "WGVGtMZHwzVvX1HvSFG771sskcEjJxiQNQDQRWHEh3NxvNb7kFlAXnVdRkkvhjpR\r\n"
        "GchFhTAzqmwltdWhWDEyCMKC2x/mSZvZtlZGY+g37Y72qHzidwtyW7rBetZJAgMB\r\n"
        "AAGjggGtMIIBqTAdBgNVHQ4EFgQUDyBd16FXlduSzyvQx8J3BM5ygHYwHwYDVR0j\r\n"
        "BBgwFoAUTiJUIBiV5uNu5g/6+rkS7QYXjzkwDgYDVR0PAQH/BAQDAgGGMB0GA1Ud\r\n"
        "JQQWMBQGCCsGAQUFBwMBBggrBgEFBQcDAjASBgNVHRMBAf8ECDAGAQH/AgEAMHYG\r\n"
        "CCsGAQUFBwEBBGowaDAkBggrBgEFBQcwAYYYaHR0cDovL29jc3AuZGlnaWNlcnQu\r\n"
        "Y29tMEAGCCsGAQUFBzAChjRodHRwOi8vY2FjZXJ0cy5kaWdpY2VydC5jb20vRGln\r\n"
        "aUNlcnRHbG9iYWxSb290RzIuY3J0MHsGA1UdHwR0MHIwN6A1oDOGMWh0dHA6Ly9j\r\n"
        "cmwzLmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5jcmwwN6A1oDOG\r\n"
        "MWh0dHA6Ly9jcmw0LmRpZ2ljZXJ0LmNvbS9EaWdpQ2VydEdsb2JhbFJvb3RHMi5j\r\n"
        "cmwwHQYDVR0gBBYwFDAIBgZngQwBAgEwCAYGZ4EMAQICMBAGCSsGAQQBgjcVAQQD\r\n"
        "AgEAMA0GCSqGSIb3DQEBDAUAA4IBAQAlFvNh7QgXVLAZSsNR2XRmIn9iS8OHFCBA\r\n"
        "WxKJoi8YYQafpMTkMqeuzoL3HWb1pYEipsDkhiMnrpfeYZEA7Lz7yqEEtfgHcEBs\r\n"
        "K9KcStQGGZRfmWU07hPXHnFz+5gTXqzCE2PBMlRgVUYJiA25mJPXfB00gDvGhtYa\r\n"
        "+mENwM9Bq1B9YYLyLjRtUz8cyGsdyTIG/bBM/Q9jcV8JGqMU/UjAdh1pFyTnnHEl\r\n"
        "Y59Npi7F87ZqYYJEHJM2LGD+le8VsHjgeWX2CJQko7klXvcizuZvUEDTjHaQcs2J\r\n"
        "+kPgfyMIOY1DMJ21NxOJ2xPRC/wAh/hzSBRVtoAnyuxtkZ4VjIOh\r\n"
        "-----END CERTIFICATE-----\r\n";
    ```

1. Adiciona uma constante a este ficheiro para o URL de fala sem a localiza√ß√£o. Este ser√° combinado com a localiza√ß√£o e o idioma mais tarde para obter o URL completo.

    ```cpp
    const char *SPEECH_URL = "https://%s.stt.speech.microsoft.com/speech/recognition/conversation/cognitiveservices/v1?language=%s";
    ```

1. No ficheiro de cabe√ßalho `speech_to_text.h`, na sec√ß√£o `private` da classe `SpeechToText`, define um campo para um cliente WiFi usando o certificado de fala:

    ```cpp
    WiFiClientSecure _speech_client;
    ```

1. No m√©todo `init`, define o certificado neste cliente WiFi:

    ```cpp
    _speech_client.setCACert(SPEECH_CERTIFICATE);
    ```

1. Adiciona o seguinte c√≥digo √† sec√ß√£o `public` da classe `SpeechToText` para definir um m√©todo para converter fala em texto:

    ```cpp
    String convertSpeechToText()
    {
        
    }
    ```

1. Adiciona o seguinte c√≥digo a este m√©todo para criar um cliente HTTP usando o cliente WiFi configurado com o certificado de fala e usando o URL de fala definido com a localiza√ß√£o e o idioma:

    ```cpp
    char url[128];
    sprintf(url, SPEECH_URL, SPEECH_LOCATION, LANGUAGE);

    HTTPClient httpClient;
    httpClient.begin(_speech_client, url);
    ```

1. Alguns cabe√ßalhos precisam de ser definidos na conex√£o:

    ```cpp
    httpClient.addHeader("Authorization", String("Bearer ") + _access_token);
    httpClient.addHeader("Content-Type", String("audio/wav; codecs=audio/pcm; samplerate=") + String(RATE));
    httpClient.addHeader("Accept", "application/json;text/xml");
    ```

    Isto define cabe√ßalhos para a autoriza√ß√£o usando o token de acesso, o formato de √°udio usando a taxa de amostragem e define que o cliente espera o resultado como JSON.

1. Ap√≥s isto, adiciona o seguinte c√≥digo para fazer a chamada √† API REST:

    ```cpp
    Serial.println("Sending speech...");

    FlashStream stream;
    int httpResponseCode = httpClient.sendRequest("POST", &stream, BUFFER_SIZE);

    Serial.println("Speech sent!");
    ```

    Isto cria um `FlashStream` e usa-o para transmitir dados para a API REST.

1. Abaixo disto, adiciona o seguinte c√≥digo:

    ```cpp
    String text = "";

    if (httpResponseCode == 200)
    {
        String result = httpClient.getString();
        Serial.println(result);

        DynamicJsonDocument doc(1024);
        deserializeJson(doc, result.c_str());

        JsonObject obj = doc.as<JsonObject>();
        text = obj["DisplayText"].as<String>();
    }
    else if (httpResponseCode == 401)
    {
        Serial.println("Access token expired, trying again with a new token");
        _access_token = getAccessToken();
        return convertSpeechToText();
    }
    else
    {
        Serial.print("Failed to convert text to speech - error ");
        Serial.println(httpResponseCode);
    }
    ```

    Este c√≥digo verifica o c√≥digo de resposta.

    Se for 200, o c√≥digo para sucesso, ent√£o o resultado √© recuperado, decodificado de JSON, e a propriedade `DisplayText` √© definida na vari√°vel `text`. Esta √© a propriedade onde a vers√£o em texto da fala √© retornada.

    Se o c√≥digo de resposta for 401, ent√£o o token de acesso expirou (estes tokens s√≥ duram 10 minutos). Um novo token de acesso √© solicitado e a chamada √© feita novamente.

    Caso contr√°rio, um erro √© enviado ao monitor serial e o `text` √© deixado em branco.

1. Adiciona o seguinte c√≥digo ao final deste m√©todo para fechar o cliente HTTP e retornar o texto:

    ```cpp
    httpClient.end();

    return text;
    ```

1. Em `main.cpp`, chama este novo m√©todo `convertSpeechToText` na fun√ß√£o `processAudio`, depois regista a fala no monitor serial:

    ```cpp
    String text = speechToText.convertSpeechToText();
    Serial.println(text);
    ```

1. Compila este c√≥digo, carrega-o no teu Wio Terminal e testa-o atrav√©s do monitor serial. Assim que vires `Ready` no monitor serial, pressiona o bot√£o C (o que est√° do lado esquerdo, mais pr√≥ximo do interruptor de energia) e fala. Ser√£o capturados 4 segundos de √°udio, que ser√£o convertidos em texto.

    ```output
    --- Available filters and text transformations: colorize, debug, default, direct, hexlify, log2file, nocontrol, printable, send_on_enter, time
    --- More details at http://bit.ly/pio-monitor-filters
    --- Miniterm on /dev/cu.usbmodem1101  9600,8,N,1 ---
    --- Quit: Ctrl+C | Menu: Ctrl+T | Help: Ctrl+T followed by Ctrl+H ---
    Connecting to WiFi..
    Connected!
    Got access token.
    Ready.
    Starting recording...
    Finished recording
    Sending speech...
    Speech sent!
    {"RecognitionStatus":"Success","DisplayText":"Set a 2 minute and 27 second timer.","Offset":4700000,"Duration":35300000}
    Set a 2 minute and 27 second timer.
    ```

> üíÅ Podes encontrar este c√≥digo na pasta [code-speech-to-text/wio-terminal](../../../../../6-consumer/lessons/1-speech-recognition/code-speech-to-text/wio-terminal).

üòÄ O teu programa de fala para texto foi um sucesso!

**Aviso Legal**:  
Este documento foi traduzido utilizando o servi√ßo de tradu√ß√£o por IA [Co-op Translator](https://github.com/Azure/co-op-translator). Embora nos esforcemos para garantir a precis√£o, √© importante notar que tradu√ß√µes autom√°ticas podem conter erros ou imprecis√µes. O documento original na sua l√≠ngua nativa deve ser considerado a fonte autorit√°ria. Para informa√ß√µes cr√≠ticas, recomenda-se a tradu√ß√£o profissional realizada por humanos. N√£o nos responsabilizamos por quaisquer mal-entendidos ou interpreta√ß√µes incorretas decorrentes do uso desta tradu√ß√£o.